#[
  MITSO Parser - –ø–∞—Ä—Å–µ—Ä —Ä–∞—Å–ø–∏—Å–∞–Ω–∏—è –∑–∞–Ω—è—Ç–∏–π –ú–ò–¢–°–û
  Copyright (C) 2022 bit0r1n

    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <https://www.gnu.org/licenses/>.
]#

import std/[
  asyncdispatch, httpclient, net,
  xmltree, strtabs,
  uri, options, strformat,
  algorithm, sequtils, strutils,
  times, json, nre, base64,
  os
]
import private/[utils, constants], typedefs, helpers
import malebolgia, pkg/htmlparser

proc loadPage*(site: ScheduleSite) {.async.} =
  ## –ü–æ–ª—É—á–µ–Ω–∏–µ –∏ —Å–æ—Ö—Ä–∞–Ω–µ–Ω–∏–µ –∫–æ–Ω—Ç–µ–Ω—Ç–∞ —Å–∞–π—Ç–∞, –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ CSRF —Ç–æ–∫–µ–Ω–∞
  let client = newAsyncHttpClient()

  debug "[loadPage]", "–ü–æ–ª—É—á–µ–Ω–∏–µ –∫–æ–Ω—Ç–µ–Ω—Ç–∞ –±–∞–∑–æ–≤–æ–π —Å—Ç—Ä–∞–Ω–∏—Ü—ã"
  let response = await client.requestWithRetry(SCHEDULE_MAIN_PAGE)

  debug "[loadPage]", "–ö–æ–Ω—Ç–µ–Ω—Ç –ø–æ–ª—É—á–µ–Ω, —Å–æ—Ö—Ä–∞–Ω–µ–Ω–∏–µ"
  site.content = some await response.body

  debug "[loadPage]", "–ü–æ–ª—É—á–µ–Ω–∏–µ –∫—É–∫–æ–≤"
  site.cookies = response.headers["Set-Cookie"]

  var doc = parseHtml(site.content.get)
  for m in doc.findAll("meta"):
    if m.attrs.hasKey("name") and m.attrs["name"] == "csrf-token":
      debug "[loadPage]", "CSRF —Ç–æ–∫–µ–Ω –ø–æ–ª—É—á–µ–Ω"
      site.csrfToken = some m.attrs["content"]
      break

  doc.clear()

  if site.csrfToken.isNone:
    raise newException(ValueError, "CSRF token not found")

proc getFaculties*(site: ScheduleSite): seq[SelectOption] =
  ## –ü–æ–ª—É—á–µ–Ω–∏–µ —Ñ–∞–∫—É–ª—å—Ç–µ—Ç–æ–≤
  debug "[getFaculties]", "–ü–∞—Ä—Å –≥–ª–∞–≤–Ω–æ–π —Å—Ç—Ä–∞–Ω–∏—Ü—ã"
  var  html = parseHtml(site.content.get)
  for select in html.findAll("select"): # –ø—Ä–æ—Ö–æ–¥ –ø–æ –ø—É–Ω–∫—Ç–∞–º —Å–µ–ª–µ–∫—Ç–∞, –æ–Ω–∏ –¥–æ—Å—Ç—É–ø–Ω—ã –ø—Ä–∏ –∑–∞–≥—Ä—É–∑–∫–µ —Å—Ç—Ä–∞–Ω–∏—Ü—ã
    if select.attrs.hasKey("id") and select.attrs["id"] == "faculty-id":
      for x in select.items:
        if x.kind == xnElement and x.attr("value") != "":
          let facult = (id: x.attr("value"), display: x.innerText)
          debug "[getFaculties]", "–ù–∞–π–¥–µ–Ω —Ñ–∞–∫—É–ª—å—Ç–µ—Ç", $facult
          result.add(facult)
  html.clear()

proc threadParseCourse(faculty, form, course, csrfToken, cookies: string): seq[Group] =
  let client = newHttpClient(headers = newHttpHeaders({
      "Content-Type": "application/x-www-form-urlencoded",
      "X-CSRF-Token": csrfToken,
      "Cookie": cookies
    })
  )

  debug "[threadParseCourse]", fmt"–ü–æ–ª—É—á–µ–Ω–∏–µ –≥—Ä—É–ø–ø ({$course}, {faculty}, {$form})"
  let groupsRawJson = client.requestWithRetry(parseUri(SCHEDULE_GROUP), HttpPost,
      body = encodeQuery({
      "depdrop_parents[0]": faculty,
      "depdrop_parents[1]": form,
      "depdrop_parents[2]": course,
      "depdrop_all_params[faculty-id]": faculty,
      "depdrop_all_params[form-id]": form,
      "depdrop_all_params[course-id]": course
    }))

  if "application/json" notin groupsRawJson.headers["content-type", 0]:
    debug "[threadParseCourse]", "–°–µ—Ä–≤–µ—Ä –æ—Ç–ø—Ä–∞–≤–∏–ª –∫–æ–Ω—Ç–µ–Ω—Ç –Ω–µ —Å –æ–∂–∏–¥–∞–µ–º—ã–º —Ç–∏–ø–æ–º –∫–æ–Ω—Ç–µ–Ω—Ç–∞",
      " ", groupsRawJson.status, " ", groupsRawJson.body
    raise newException(ScheduleServiceError, "Schedule service (groups) responded with wrong Content-Type")

  let groupsJson = parseJson(groupsRawJson.body)

  for groupElem in groupsJson["output"]:
    result.add(Group(
      id: groupElem["id"].getStr(),
      display: groupElem["name"].getStr(),
      course: parseCourse(course),
      form: parseForm(form),
      faculty: parseFaculty(faculty)
    ))
    debug "[threadParseCourse]", "–ù–∞–π–¥–µ–Ω–∞ –≥—Ä—É–ø–ø–∞", $result[^1]

proc threadParseForm(faculty, form, csrfToken, cookies: string, sleepTime: int): seq[Group] =
  let client = newHttpClient(headers = newHttpHeaders({
        "Content-Type": "application/x-www-form-urlencoded",
        "X-CSRF-Token": csrfToken,
        "Cookie": cookies
      })
    )

  debug "[threadParseForm]", fmt"–ü–æ–ª—É—á–µ–Ω–∏–µ –∫—É—Ä—Å–æ–≤ –¥–ª—è —Ñ–∞–∫—É–ª—å—Ç–µ—Ç–∞ {faculty} ({$form})"
  let coursesRawJson = client.requestWithRetry(parseUri(SCHEDULE_COURSE),
        HttpPost,
      body = encodeQuery({
      "depdrop_parents[0]": faculty,
      "depdrop_parents[1]": form,
      "depdrop_all_params[faculty-id]": faculty,
      "depdrop_all_params[form-id]": form,
    }))

  if "application/json" notin coursesRawJson.headers["content-type", 0]:
    debug "[threadParseForm]", "–°–µ—Ä–≤–µ—Ä –æ—Ç–ø—Ä–∞–≤–∏–ª –∫–æ–Ω—Ç–µ–Ω—Ç –Ω–µ —Å –æ–∂–∏–¥–∞–µ–º—ã–º —Ç–∏–ø–æ–º –∫–æ–Ω—Ç–µ–Ω—Ç–∞",
      " ", coursesRawJson.status, " ", coursesRawJson.body
    raise newException(ScheduleServiceError, "Schedule service (courses) responded with wrong Content-Type")

  let coursesJson = parseJson(coursesRawJson.body)
  var courses = newSeq[SelectOption]()

  # –ü–∞—Ä—Å –∫—É—Ä—Å–æ–≤ —Ñ–æ—Ä–º—ã –æ–±—É—á–µ–Ω–∏—è
  for course in coursesJson["output"].getElems():
    courses.add((id: course["id"].getStr, display: course["name"].getStr))

  # –ü—Ä–æ—Ö–æ–¥ –ø–æ –∫—É—Ä—Å–∞–º
  var
    coursesGroups = newSeq[seq[Group]](courses.len)
    m = createMaster()

  m.awaitAll:
    for i, course in courses:
      m.spawn threadParseCourse(faculty = faculty, form = form, course = course.id,
        csrfToken = csrfToken, cookies = cookies) -> coursesGroups[i]
      if sleepTime > 0: sleep(sleepTime)

  courses.setLen(0)

  for groups in coursesGroups:
    result.add(groups)

  coursesGroups.setLen(0)

proc threadParseFaculty(faculty, csrfToken, cookies: string, sleepTime: int): seq[Group] =
  let client = newHttpClient(headers = newHttpHeaders({
        "Content-Type": "application/x-www-form-urlencoded",
        "X-CSRF-Token": csrfToken,
        "Cookie": cookies
      })
    )

  debug "[threadParseFaculty]", "–ü–æ–ª—É—á–µ–Ω–∏–µ —Ñ–æ—Ä–º –æ–±—É—á–µ–Ω–∏—è –¥–ª—è —Ñ–∞–∫—É–ª—å—Ç–µ—Ç–∞", $faculty
  let formsRawJson = client.requestWithRetry(parseUri(SCHEDULE_FORMS), HttpPost,
    body = encodeQuery({ "depdrop_parents[0]": faculty,
      "depdrop_all_params[faculty-id]": faculty }))

  if "application/json" notin formsRawJson.headers["content-type", 0]:
    debug "[threadParseFaculty]", "–°–µ—Ä–≤–µ—Ä –æ—Ç–ø—Ä–∞–≤–∏–ª –∫–æ–Ω—Ç–µ–Ω—Ç –Ω–µ —Å –æ–∂–∏–¥–∞–µ–º—ã–º —Ç–∏–ø–æ–º –∫–æ–Ω—Ç–µ–Ω—Ç–∞",
      " ", formsRawJson.status, " ", formsRawJson.body
    raise newException(ScheduleServiceError, "Schedule service (forms) responded with wrong Content-Type")

  let formsJson = parseJson(formsRawJson.body)
  var forms = newSeq[SelectOption]()

  # –ü–∞—Ä—Å —Ñ–æ—Ä–º –æ–±—É—á–µ–Ω–∏—è
  for form in formsJson["output"].getElems():
    forms.add((form["id"].getStr(), form["name"].getStr()))

  # –ü—Ä–æ—Ö–æ–¥ –ø–æ —Ñ–æ—Ä–º–∞–º –æ–±—É—á–µ–Ω–∏—è
  var
    formsGroups = newSeq[seq[Group]](forms.len)
    m = createMaster()

  m.awaitAll:
    for i, form in forms:
      m.spawn threadParseForm(faculty = faculty, form = form.id,
        csrfToken = csrfToken, cookies = cookies, sleepTime = sleepTime) -> formsGroups[i]
      if sleepTime > 0: sleep(sleepTime)

  forms.setLen(0)

  for groups in formsGroups:
    result.add(groups)

  formsGroups.setLen(0)

proc getGroups*(site: ScheduleSite, faculties: seq[SelectOption], sleepTime = 6000): seq[Group] {.gcsafe.} =
  ## –ü–æ–ª—É—á–µ–Ω–∏–µ –≥—Ä—É–ø–ø

  # –ü—Ä–æ—Ö–æ–¥ –ø–æ —Ñ–∞–∫—É–ª—å—Ç–µ—Ç–∞–º
  var
    facultiesGroups = newSeq[seq[Group]](faculties.len)
    m = createMaster()

  m.awaitAll:
    for i, faculty in faculties:
      m.spawn threadParseFaculty(faculty = faculty.id,
        csrfToken = site.csrfToken.get, cookies = site.cookies.toFullString, sleepTime = sleepTime) -> facultiesGroups[i]

  var resultGroups = newSeq[Group]()
  for groups in facultiesGroups:
    resultGroups.add(groups)

  facultiesGroups.setLen(0)

  # –°–æ—Ä—Ç–∏—Ä–æ–≤–∫–∞ –≥—Ä—É–ø–ø –ø–æ –∫—É—Ä—Å–∞–º/–Ω–æ–º–µ—Ä–∞–º
  debug "[getGroups]", "–°–æ—Ä—Ç–∏—Ä–æ–≤–∫–∞ –≥—Ä—É–ø–ø –ø–æ –∫—É—Ä—Å–∞–º –∏ –Ω–æ–º–µ—Ä–∞–º"
  resultGroups.sort do (x, y: Group) -> int:
    result = cmp(x.course, y.course)
    if result == 0:
      result = cmp(x.id, y.id)

  #[
    –§–∏–ª—å—Ç—Ä–∞—Ü–∏—è –≥—Ä—É–ø–ø
    –í —Ä–∞—Å–ø–∏—Å–∞–Ω–∏–∏ –ø–æ—á–µ–º—É-—Ç–æ –¥—É–±–ª–∏—Ä—É—é—Ç—Å—è –≥—Ä—É–ø–ø—ã, –æ—Å—Ç–∞–≤–∞—è—Å—å –Ω–∞ –ø—Ä–µ–¥—ã–¥—É—â–∏—Ö –∫—É—Ä—Å–∞—Ö.
    –ï—Å–ª–∏ –Ω–∞—Ö–æ–¥—è—Ç—Å—è –¥—É–±–ª–∏—Ä—É—é—â–∏–µ –≥—Ä—É–ø–ø—ã - –æ—Å—Ç–∞–µ—Ç—Å—è —Ç–æ–ª—å–∫–æ –∏–∑ –ø–æ—Å–ª–µ–¥–Ω–µ–≥–æ –≤–æ–∑–º–æ–∂–Ω–æ–≥–æ –∫—É—Ä—Å–∞
  ]#
  debug "[getGroups]", "–§–∏–ª—å—Ç—Ä–∞—Ü–∏—è –≥—Ä—É–ø–ø"
  result = resultGroups.filter do (x: Group) -> bool:
    if x.id == INVALID_GROUP_ID: # ???
      debug "[getGroups]", "omgomgüò± its fkin AUDITORIYA group, group of my dreams üòçü§©‚ô•"
      return false # üëé btw
    var simGroups = resultGroups.filter do (y: Group) -> bool: result = y.id ==
        x.id and y.faculty == x.faculty
    if simGroups.len == 1:
      result = true
    else:
      debug "[getGroups]", "–ù–∞–π–¥–µ–Ω–æ –Ω–µ—Å–∫–æ–ª—å–∫–æ –ø–æ—Ö–æ–∂–∏—Ö –≥—Ä—É–ø–ø", $simGroups
      simGroups.sort do (y, z: Group) -> int: result = cmp(y.course, z.course)
      result = simGroups[^1] == x

  resultGroups.setLen(0)

proc loadGroups*(site: ScheduleSite, sleepTime = 6000): Future[seq[Group]] {.async.} =
  ## –•–µ–ª–ø–µ—Ä, –∑–∞–≥—Ä—É–∂–∞—é—â–∏–π –≤—Å–µ –≥—Ä—É–ø–ø—ã —Å –Ω—É–ª—è
  debug "[loadGroups]", "–ó–∞–≥—Ä—É–∑–∫–∞ —Å—Ç—Ä–∞–Ω–∏—Ü—ã"
  await site.loadPage()
  debug "[loadGroups]", "–ü–∞—Ä—Å —Ñ–∞–∫—É–ª—å—Ç–µ—Ç–æ–≤"
  let faculties = site.getFaculties()
  debug "[loadGroups]", "–ü–∞—Ä—Å –≥—Ä—É–ø–ø"
  result = site.getGroups(faculties, sleepTime)

proc getWeeks*(site: ScheduleSite, group: Group): Future[seq[SelectOption]] {.async, gcsafe.} =
  ## –ü–æ–ª—É—á–µ–Ω–∏–µ –¥–æ—Å—Ç—É–ø–Ω—ã—Ö –Ω–µ–¥–µ–ª—å –¥–ª—è –≥—Ä—É–ø–ø—ã
  let client = newAsyncHttpClient(headers = newHttpHeaders({
        "Content-Type": "application/x-www-form-urlencoded",
        "X-CSRF-Token": site.csrfToken.get,
        "Cookie": site.cookies.toFullString
      })
    )

  debug "[getWeeks]", "–ü–æ–ª—É—á–µ–Ω–∏–µ –¥–æ—Å—Ç—É–ø–Ω—ã—Ö –Ω–µ–¥–µ–ª—å –¥–ª—è –≥—Ä—É–ø–ø—ã", $group
  let
    weeksRawJson = await client.requestWithRetry(parseUri(SCHEDULE_WEEK),
        HttpPost,
      body = encodeQuery({
      "depdrop_parents[0]": %group.faculty,
      "depdrop_parents[1]": %group.form,
      "depdrop_parents[2]": %group.course,
      "depdrop_parents[3]": group.id,
      "depdrop_all_params[faculty-id]": %group.faculty,
      "depdrop_all_params[form-id]": %group.form,
      "depdrop_all_params[course-id]": %group.course,
      "depdrop_all_params[group-id]": group.id,
    }))

  if "application/json" notin weeksRawJson.headers["content-type", 0]:
    debug "[getWeeks]", "–°–µ—Ä–≤–µ—Ä –æ—Ç–ø—Ä–∞–≤–∏–ª –∫–æ–Ω—Ç–µ–Ω—Ç –Ω–µ —Å –æ–∂–∏–¥–∞–µ–º—ã–º —Ç–∏–ø–æ–º –∫–æ–Ω—Ç–µ–Ω—Ç–∞",
      " ", weeksRawJson.status, " ", await  weeksRawJson.body
    raise newException(ScheduleServiceError, "Schedule service (weeks) responded with wrong Content-Type")

  let
    resp = await weeksRawJson.body()
    weeksJson = parseJson(resp)

  if weeksJson["output"].len == 0:
    debug "[getWeeks]", "–ù–µ –Ω–∞—à–ª–æ—Å—å –¥–æ—Å—Ç—É–ø–Ω—ã—Ö –Ω–µ–¥–µ–ª—å –¥–ª—è", $group
    return @[]

  for week in weeksJson["output"]:
    let weekId = week["id"].getInt()
    result.add(($weekId, if weekId > 0: $(weekId + 1) &
        " –Ω–µ–¥–µ–ª—è" else: "–¢–µ–∫—É—â–∞—è –Ω–µ–¥–µ–ª—è"))

  debug "[getWeeks]", "–ü–æ–ª—É—á–µ–Ω—ã –Ω–µ–¥–µ–ª–∏ –¥–ª—è –≥—Ä—É–ø–ø—ã",
      $group, $result

proc getSchedule*(site: ScheduleSite, group: Group, week: string): Future[seq[
    ScheduleDay]] {.async.} =
  # –ü–æ–ª—É—á–µ–Ω–∏–µ —Ä–∞—Å–ø–∏—Å–∞–Ω–∏—è –Ω–∞ –Ω–µ–¥–µ–ª—é
  let client = newAsyncHttpClient(headers = newHttpHeaders({
        "Content-Type": "application/x-www-form-urlencoded",
        "X-CSRF-Token": site.csrfToken.get,
        "Cookie": site.cookies.toFullString
      })
    )
  debug "[getSchedule]", fmt"–ü–æ–ª—É—á–µ–Ω–∏–µ —Ä–∞—Å–ø–∏—Å–∞–Ω–∏—è –¥–ª—è –≥—Ä—É–ø–ø—ã {$group} –¥–ª—è {$week}"
  var
    sheldueRawHtml = await client.requestWithRetry(SCHEDULE_MAIN_PAGE, HttpPost,
      body = encodeQuery({
        "ScheduleSearch[fak]": %group.faculty,
        "ScheduleSearch[form]": %group.form,
        "ScheduleSearch[kurse]": %group.course,
        "ScheduleSearch[group_class]": group.id,
        "ScheduleSearch[week]": week,
      }))
    resp = await sheldueRawHtml.body()
    scheduleHtml = parseHtml(resp)

  var weeksContainer = scheduleHtml.findAll("div").filterIt(it.attr("id") == "schedule-content")[0]

  var weekContainerIndex = -1
  for el in weeksContainer.items:
    weekContainerIndex += 1

    if weekContainerIndex != parseInt(week): continue # –¥–ª—è –æ–±—Ä–∞—Ç–Ω–æ–π —Å–æ–≤–º–µ—Å—Ç–∏–º–æ—Å—Ç–∏ —Å–æ—Ö—Ä–∞–Ω—è–µ—Ç—Å—è –ª–æ–≥–∏–∫–∞: –æ–¥–∏–Ω –≤—ã–∑–æ–≤ - –ø–æ–ª—É—á–µ–Ω–∏–µ –æ–¥–Ω–æ–π –Ω–µ–¥–µ–ª–∏

    var day: ScheduleDay
    var eI = -1 # –∏–Ω–¥–µ–∫—Å –±–ª–æ–∫–∞ –¥–Ω—è
    for item in el.items:
      if item.kind != xnElement: continue

      if item.tag == "h2": # –±–ª–æ–∫ –¥–Ω—è –Ω–∞—á–∏–Ω–∞–µ—Ç—Å—è —Å –∑–∞–≥–æ–ª–æ–≤–∫–∞ - –¥–∞—Ç—ã
        eI += 1

        day = ScheduleDay(
          displayDate: item.innerText,
          day: parseDay(eI),
          lessons: newSeq[Lesson]()
        )

        let
          scheduleDayMonth = parseMonth(item.innerText.split(" ")[^1])
          yearOffset = if scheduleDayMonth == mJan and
            result.filterIt(it.date.month == mDec).len != 0: 1 else: 0
          dayTime = dateTime(
            year = now().year + yearOffset,
            month = scheduleDayMonth,
            monthday = parseInt(item.innerText.split(" ")[1]),
            zone = utc()
          )
        day.date = dayTime
      elif item.tag == "div" and item.attr("class") == "table-responsive": # —Ç–∞–±–ª–∏—Ü–∞ –∑–∞–Ω—è—Ç–∏–π
        let lessonsTable = item.findAll("table")[0]
        let trs = lessonsTable.findAll("tr").filter do (x: XmlNode) -> bool: x.kind == xnElement
        for i, trDay in trs: # –ø—Ä–æ—Ö–æ–¥ –ø–æ —Å—Ç—Ä–æ–∫–∞–º –∑–∞–Ω—è—Ç–∏–π
          if trDay.kind != xnElement: continue
          if i == 0: continue # –∏–≥–Ω–æ—Ä –∏–∑ thead
          var lesson = Lesson()
          let tds = trDay.findAll("td").filter do (x: XmlNode) ->
              bool: x.kind == xnElement
          if tds[1].innerText.contains("(–Ω–µ—Ç –∑–∞–Ω—è—Ç–∏–π)") or tds[
              1].innerText.replace("\n", " ").match(
              re"^\d\. -[ ]?$").isSome: continue # –∏–≥–Ω–æ—Ä –ª–∞–±/–ø—Ä–∞–∫ –¥–ª—è –æ–¥–Ω–æ–π —á–∞—Å—Ç–∏

          var
            ls = parseLessonName(tds[1].innerText.replace("\n", " "))
            classrooms = if tds[2].innerText.len > 0 and encode(tds[2].innerText) != "wqA=":
              parseClassrooms(tds[2].innerText) else: @[]
            time: LessonTime

          try:
            time = parseTime(tds[0].innerText) # =))
          except ValueError:
            continue

          if day.lessons.len != 0 and ls.lessonName == day.lessons[
              ^1].name and
            ls.lessonType == day.lessons[^1].lType and time == day.lessons[
                ^1].lessonTime: # –∑–∞–Ω—è—Ç–∏–µ —Ä–∞–∑–¥–µ–ª–µ–Ω–æ –Ω–∞ –≥—Ä—É–ø–ø—ã
            for classroom in classrooms:
              if classroom notin day.lessons[^1].classrooms:
                day.lessons[^1].classrooms.add(classroom)
            if ls.teacher notin INVALID_TEACHERS and ls.teacher notin day.lessons[^1].teachers:
              day.lessons[^1].teachers.add(ls.teacher)
          else:
            lesson.name = ls.lessonName
            lesson.lType = ls.lessonType
            if ls.teacher notin INVALID_TEACHERS: lesson.teachers.add(ls.teacher)
            if classrooms.len > 0: lesson.classrooms.add(
                classrooms)
            lesson.lessonTime = time

            var lessonDate = day.date
            lessonDate += initDuration(hours = ($%lesson.lessonTime).hours -
                3, minutes = ($%lesson.lessonTime).minutes)

            lesson.date = lessonDate

            day.lessons.add(lesson)
        if day.lessons.len > 0:
          day.lessons.sort do (x, y: Lesson) -> int: cmp(x.lessonTime, y.lessonTime)
          result.add(day)

proc getSchedule*(site: ScheduleSite, group: Group, week: SelectOption): Future[seq[
    ScheduleDay]] {.async.} =
  result = await site.getSchedule(group, week.id)
